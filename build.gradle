plugins {
    id 'java'
    id 'distribution'
}

group 'net.moasdawiki'
version '2.5.1'

repositories {
    mavenCentral()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    compileOnly "org.jetbrains:annotations:20.1.0"
    testImplementation "org.jetbrains:annotations:20.1.0"
    testImplementation "org.testng:testng:7.3.0"
    testImplementation "org.mockito:mockito-core:3.+"
}

jar {
    manifest {
        attributes(
                'Main-Class': "net.moasdawiki.Main",
                'Created-By': "Gradle ${gradle.gradleVersion}",
                'Implementation-Vendor': "Herbert Reiter",
                'Implementation-URL': "https://moasdawiki.net/",
                'Implementation-Version': project.version
        )
    }
}

distributions {
    main {
        contents {
            from jar
            exclude("**/*.cache")
            exclude("**/*.config")
            from 'LICENSE'
            from 'README.md'
            into('lib') {
                from project.configurations.runtime
            }
        }
    }
}
distTar.enabled = false

test{
    useTestNG() {
        useDefaultListeners = true
    }
    reports.html.enabled = false
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
